# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/device_security_level/common/dslm.gni")
import("//build/ohos.gni")

# service_common object
ohos_source_set("service_common_obj") {
  sources = [
    "dslm_crypto.c",
    "dslm_msg_serialize.c",
  ]

  include_dirs = [
    "../include",
    "../../interfaces/inner_api/include",
  ]

  deps = [ "../..//baselib/utils:utils_static" ]

  if (defined(ohos_lite)) {
    if (ohos_kernel_type == "linux" || ohos_kernel_type == "liteos_a") {
      deps += [
        "$dslm_ipc_path/interfaces/innerkits/c/ipc:ipc_single",
        "//third_party/openssl/ohos_lite:openssl_shared",
      ]
      external_deps = [ "hilog_lite:hilog_shared" ]
    } else if (ohos_kernel_type == "liteos_m") {
      deps += [
        "$dslm_hilog_path/featured:hilog_static",
        "//third_party/openssl/ohos_lite:openssl_static",
      ]
    }
  } else {
    sanitize = {
      integer_overflow = true
      ubsan = true
      boundary_sanitize = true
      cfi = true
      cfi_cross_dso = true
      blocklist = "../../cfi_blocklist.txt"
    }
    branch_protector_ret = "pac_ret"

    deps += [ "//third_party/openssl:libcrypto_shared" ]

    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "ipc:ipc_core",
    ]
  }

  configs = [
    "../../common:common_configs",
    "../../common:common_lite_configs",
  ]

  part_name = "device_security_level"
  subsystem_name = "security"
}
