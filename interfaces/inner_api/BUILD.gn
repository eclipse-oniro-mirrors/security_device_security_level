# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

config("device_security_level_sdk_config") {
  include_dirs = [ "include" ]
}

group("fwk_group") {
  if (os_level == "standard") {
    deps = [ ":dslm_sdk" ]
  } else if (os_level == "small") {
    deps = [ ":dslm_sdk_small" ]
  }
}

if (os_level == "standard") {
  ohos_shared_library("dslm_sdk") {
    sources = [
      "src/standard/device_security_info.cpp",
      "src/standard/device_security_level_callback_helper.cpp",
      "src/standard/device_security_level_callback_stub.cpp",
      "src/standard/device_security_level_loader.cpp",
      "src/standard/device_security_level_proxy.cpp",
    ]

    include_dirs = [
      "../../services/include",
      "../../common/include",
    ]

    configs = [ "../../common:common_configs" ]

    public_configs = [ ":device_security_level_sdk_config" ]

    external_deps = [
      "c_utils:utils",
      "hilog:libhilog",
      "ipc:ipc_core",
      "samgr:samgr_proxy",
    ]

    part_name = "device_security_level"
    subsystem_name = "security"
  }
} else if (os_level == "small") {
  ohos_shared_library("dslm_sdk_small") {
    sources = [
      "src/lite/device_security_info.c",
      "src/lite/device_security_level_proxy.c",
    ]

    include_dirs = [
      "../../services/include",
      "../../common/include",
    ]

    configs = [
      "../../common:common_configs",
      "../../common:common_lite_configs",
    ]

    public_configs = [ ":device_security_level_sdk_config" ]

    deps = [
      "../../baselib/utils:utils_static",
      "//base/hiviewdfx/hilog_lite/frameworks/featured:hilog_shared",
      "//foundation/communication/ipc/interfaces/innerkits/c/ipc:ipc_single",
      "//foundation/systemabilitymgr/samgr_lite/samgr:samgr",
    ]

    part_name = "device_security_level"
    subsystem_name = "security"
  }

  group("dslm_sdk") {
  }
}
