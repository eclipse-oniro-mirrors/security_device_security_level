# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//base/security/device_security_level/common/dslm.gni")
import("//build/ohos.gni")

# messenger_static lib
config("messenger_static_config") {
  include_dirs = [ "include" ]
}

ohos_static_library("messenger_static") {
  include_dirs = [
    "include",
    "src/common",
    "src/utils",
  ]

  sources = [
    "src/common/messenger.c",
    "src/utils/messenger_utils.c",
  ]

  deps = [ "../utils:utils_static" ]

  if (defined(ohos_lite)) {
    sources += [
      "src/lite/messenger_device_session_manager.c",
      "src/lite/messenger_device_status_manager.c",
      "src/lite/messenger_impl.c",
    ]
    if (ohos_kernel_type == "linux" || ohos_kernel_type == "liteos_a") {
      deps += [
        "$dslm_dsoftbus_path/sdk:softbus_client",
        "$dslm_hilog_path/featured:hilog_shared",
      ]
    } else if (ohos_kernel_type == "liteos_m") {
      deps += [
        "$dslm_dsoftbus_path/sdk:softbus_client",
        "$dslm_hilog_path/featured:hilog_static",
      ]
    }
  } else {
    sources += [
      "src/standard/messenger_device_socket_manager.c",
      "src/standard/messenger_device_status_manager.cpp",
      "src/standard/messenger_impl.c",
    ]
    sanitize = {
      integer_overflow = true
      ubsan = true
      boundary_sanitize = true
      cfi = true
      cfi_cross_dso = true
      blocklist = "../../cfi_blocklist.txt"
    }
    branch_protector_ret = "pac_ret"

    external_deps = [
      "c_utils:utils",
      "device_manager:devicemanagersdk",
      "dsoftbus:softbus_client",
      "hilog:libhilog",
    ]
  }

  configs = [
    "../../common:common_configs",
    "../../common:common_lite_configs",
  ]

  public_configs = [ ":messenger_static_config" ]
  part_name = "device_security_level"
  subsystem_name = "security"
}
